import theme.Json
import theme.Xml

import java.nio.charset.StandardCharsets

plugins {
	id 'java'
	id 'org.jetbrains.intellij' version '0.6.5'
}

group 'com.github.adamwojszczyk'
version '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
}

intellij {
	version '2020.3'
}

patchPluginXml {
	pluginDescription = """
    	<h3>Gruvbox Light Hard Black Theme</h3>
    	<br>
    	<a href="https://github.com/adamwojszczyk/gruvbox-light-soft-black-theme">On GitHub</a> | <a href="https://github.com/adamwojszczyk/gruvbox-light-soft-black-theme/issues">Report an issue</a>
    	<br><br><br>
    	<img src="https://raw.githubusercontent.com/adamwojszczyk/gruvbox-light-soft-black-theme/master/theme/screenshots/main-window.png" border="0" alt="Main window screenshot">
	"""
	changeNotes = """
		<i><b>0.0.1</b></i>
		<ul>
			<li>initial release</li>
		</ul>
	"""
}

publishPlugin {
	channels 'stable'
	token = System.getenv('ORG_GRADLE_PROJECT_intellijPublishToken')
}

task generatePluginXml(type: Copy, group: "theme") {
	outputs.upToDateWhen { false }
	from "$projectDir/src/main/templates/"
	expand(new theme.Map(new theme.Plugin()).string())
	into "$projectDir/src/main/resources/"
}

task generateIntelliJTheme(group: "theme") {
	outputs.upToDateWhen { false }
	doLast {
		File file = file("$projectDir/src/main/resources/intellij.theme.json")
		file.setText(new Json(new theme.Plugin().theme).text(), StandardCharsets.UTF_8.name())
	}
}

task generateIntelliJScheme(group: "theme") {
	outputs.upToDateWhen { false }
	doLast {
		File file = file("$projectDir/src/main/resources/generated.intellij.scheme.xml")
		file.setText(new Xml(new theme.Plugin().scheme).text(), StandardCharsets.UTF_8.name())
	}
}

task generateTheme(group: "theme") {
	dependsOn(generatePluginXml, generateIntelliJTheme, generateIntelliJScheme)
}

buildPlugin.dependsOn(generateTheme)
